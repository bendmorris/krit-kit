import kit.sys.file;

abstract ImageLoader: Void {
    public static const TYPE = "img";

    public static function load(allocator: Box[Allocator], id: CString): Shared[Void]
    using implicit allocator {
        // shortcut for now: just load from file
        var surface = IMG_Load(id);
        // premultiply alpha
        SDL_LockSurface(surface);
        var size = surface.w * surface.h;
        var pixels: Ptr[Uint8] = surface.pixels;
        for i in 0 ... size {
            var a = pixels[i * 4 + 3];
            pixels[i * 4] = (pixels[i * 4] * a / 0xff) as Uint8;
            pixels[i * 4 + 1] = (pixels[i * 4 + 1] * a / 0xff) as Uint8;
            pixels[i * 4 + 2] = (pixels[i * 4 + 2] * a / 0xff) as Uint8;
        }
        SDL_UnlockSurface(surface);
        // upload texture
        var img: Shared[ImageData] = Shared.alloc(sizeof ImageData);
        img.dimensions.width = surface.w;
        img.dimensions.height = surface.h;
        var texture: Ptr[GLuint] = img.texture;
        glGenTextures(1, texture);
        glBindTexture(GL_TEXTURE_2D, texture);
        glTexImage2D(GL_TEXTURE_2D, 0, GL_RGBA, surface.w, surface.h, 0, GL_RGBA, GL_UNSIGNED_BYTE, surface.pixels);
        GL.checkForErrors("asset load");
        SDL_FreeSurface(surface);
        return img as Shared[Void];
    }

    public static function dispose(allocator: Box[Allocator], img: Ptr[Void]) {
        var shared = img as Shared[ImageData];
        glDeleteTextures(1, shared.texture);
        shared.free();
    }

    public static function getFileSize(id: CString) {
        var f = File.read(id);
        var size = f.getSize();
        f.close();
        return size;
    }
}
