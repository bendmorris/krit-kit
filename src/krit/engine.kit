struct Engine {
    var paused: Bool = false;
    var fps: Int;
    var fixedFrameRate: Bool;

    var onUpdate: UpdateSignal = null;
    var postUpdate: UpdateSignal = null;

    var onRender: RenderSignal = null;
    var postRender: RenderSignal = null;

    // global context
    var input: InputContext = struct InputContext;
    var asset: AssetContext = struct AssetContext;

    var bgColor: Color = Color.Black;
    var bgAlpha: Float = 1;
    var camera: Camera = struct Camera;

    var root: Box[Entity];
    var userData: Ptr[Void] = null;

    function update(context: Ptr[UpdateContext]) using implicit context {
        if this.paused {
            return;
        }
        context.asset = this.asset;
        context.camera = this.camera;
        context.input = this.input;
        this.onUpdate.invoke();
        this.camera.update();
        this.input.update();
        this.root.update();
        this.postUpdate.invoke();
    }

    function render(context: Ptr[RenderContext]) using implicit context {
        context.camera = this.camera;
        var renderer = context.renderer;
        renderer.startFrame();

        this.onRender.invoke();

        if this.bgAlpha > 0 {
            var key = struct DrawKey;
            context.renderSystem.addRect(key,
                struct Rectangle[Int] {
                    point: struct Point[Int],
                    dimensions: struct Dimensions[Int] {x: context.window.width, y: context.window.height},
                },
                struct Matrix,
                this.bgColor.withAlpha(this.bgAlpha), false
            );
        }
        context.camera = this.camera;
        this.root.render();

        this.postRender.invoke();
    }
}
