import kit.sys.file;

include "expat.h" => "expat";

// FIXME: Char won't support UTF8
typedef GlyphCharType = Char;

struct GlyphData {
    public var page: Int = 0;
    public var id: Size = 0;
    public var glyph: GlyphCharType = 0;
    public var rect: Rectangle[Int];
    public var offset: Point[Int];
    public var xAdvance: Int = 0;
}

struct BitmapFont {
    public static const TYPE = "bmfont";

    public var size: Int = 0;
    public var lineHeight: Int = 0;
    public var pages: Vector[ImageData];
    public var glyphData: Map[GlyphCharType, GlyphData];
    public var allocator: Box[Allocator];
    public var kerningTable: Map[Int, Int];

    public static function load(allocator: Box[Allocator], path: CString): Shared[Void]
    using implicit allocator {
        var shared = Shared[BitmapFont].alloc(sizeof BitmapFont);
        (*shared) = struct BitmapFont {
            pages: Vector.new(4),
            glyphData: Map.new(256),
            kerningTable: Map.new(),
            allocator
        };
        var userData: Ptr[BitmapFont] = shared;
        // font
        //   info: size
        //   common: lineheight
        //   pages
        //     page: file, id
        //   chars (+count)
        //     char: x, y, width, height, xoffset, yoffset, xadvance, page
        var parser: XML_Parser = XML_ParserCreate(null);
        XML_SetUserData(parser, userData);
        XML_SetElementHandler(parser, Self.startElement, null);
        var buf: CArray[Char, 1024];
        var fp = File.read(path);
        do {
            var readLength = fp.readBytes(buf, 1024);
            if readLength == 0 {
                break;
            }
            if XML_Parse(parser, buf, readLength, if readLength < 1024 then 1 else 0) == XML_STATUS_ERROR {
                panic("failed to parse bitmap font XML!");
            }
        } while true;
        XML_ParserFree(parser);
        return shared as Shared[Void];
    }

    public static function dispose(allocator: Box[Allocator], font: Ptr[Void]) {}

    static function startElement(userData: Ptr[Void], name: Ptr[Const[Char]], attrs: Ptr[Ptr[Const[Char]]]): Void {
        var font: Ptr[BitmapFont] = userData;
        match name {
            "kernings" => {
                var attr = attrs;
                while *attr != null {
                    if *(attr + 1) != null {
                        var key = *attr;
                        var value = *(attr + 1);
                        match key {
                            "count" => font.kerningTable.resize((atoi(value) * 1.5) as Int);
                        }
                    }
                    attr = attr + 2;
                }
            }
            "kerning" => {
                var first: Int = 0;
                var second: Int = 0;
                var amount: Int = 0;
                var attr = attrs;
                while *attr != null {
                    if *(attr + 1) != null {
                        var key = *attr;
                        var value = *(attr + 1);
                        match key {
                            "first" => first = atoi(value);
                            "second" => second = atoi(value);
                            "amount" => amount = atoi(value);
                        }
                    }
                    attr = attr + 2;
                }
                font.kerningTable[((first << 16) | second)] = amount;
            }
            "info" => {
                var attr = attrs;
                while *attr != null {
                    if *(attr + 1) != null {
                        var key = *attr;
                        var value = *(attr + 1);
                        match key {
                            "size" => font.size = atoi(value);
                        }
                    }
                    attr = attr + 2;
                }
            }
            "common" => {
                var attr = attrs;
                while *attr != null {
                    if *(attr + 1) != null {
                        var key = *attr;
                        var value = *(attr + 1);
                        match key {
                            "lineHeight" => font.lineHeight = atoi(value);
                        }
                    }
                    attr = attr + 2;
                }
            }
            "page" => {
                var attr = attrs;
                while *attr != null {
                    if *(attr + 1) != null {
                        var key = *attr;
                        var value = *(attr + 1);
                        match key {
                            "file" => {
                                using implicit font.allocator {
                                    var asset = ImageLoader.load(value);
                                    font.pages.push(asset as Ptr[ImageData]);
                                }
                            }
                        }
                    }
                    attr = attr + 2;
                }
            }
            "char" => {
                var glyphData = struct GlyphData;
                var attr = attrs;
                while *attr != null {
                    if *(attr + 1) != null {
                        var key = *attr;
                        var value = *(attr + 1);
                        match key {
                            "id" => {
                                glyphData.id = atoi(value);
                            }
                            "x" => {
                                glyphData.rect.x = atoi(value);
                            }
                            "y" => {
                                glyphData.rect.y = atoi(value);
                            }
                            "width" => {
                                glyphData.rect.width = atoi(value);
                            }
                            "height" => {
                                glyphData.rect.height = atoi(value);

                            }
                            "xoffset" => {
                                glyphData.offset.x = atoi(value);

                            }
                            "yoffset" => {
                                glyphData.offset.y = atoi(value);
                            }
                            "xadvance" => {
                                glyphData.xAdvance = atoi(value);
                            }
                            "page" => {
                                glyphData.rect.x = atoi(value);
                            }
                            "letter" => {
                                using implicit font.allocator {
                                    match value {
                                        "space" => glyphData.glyph = c' ';
                                        "&quot;" => glyphData.glyph = c'"';
                                        "&amp;" => glyphData.glyph = c'&';
                                        "&gt;" => glyphData.glyph = c'>';
                                        "&lt;" => glyphData.glyph = c'<';
                                        default => glyphData.glyph = value[0];
                                    }
                                }
                            }
                        }
                    }
                    attr = attr + 2;
                }
                if glyphData.glyph != 0 {
                    font.glyphData.put(glyphData.glyph, glyphData);
                }
            }
        }
    }

    public function getGlyph(c: Ptr[GlyphCharType]): Option[Ptr[GlyphData]] {
        return this.glyphData.ref(c);
    }

    public function getPage(i: Int): Ptr[ImageData] {
        return this.pages[i];
    }
}
