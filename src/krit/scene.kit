struct Scene {
    public var bgColor: Color = Color.Black;
    public var bgAlpha: Float = 1;
    public var camera: Camera = struct Camera;
    public var shader: Ptr[SceneShader] = null;

    public var onUpdate: Signal1[Ptr[UpdateContext]] = null;
    public var postUpdate: Signal1[Ptr[UpdateContext]] = null;
    public var onRender: Signal1[Ptr[RenderContext]] = null;
    public var postRender: Signal1[Ptr[RenderContext]] = null;

    public var entities: Vector[Box[Entity]] = Vector.new(0x100);
    public var userData: Ptr[Void] = null;

    // scene-specific context
    public var input: InputContext = struct InputContext;
    public var asset: AssetContext = struct AssetContext;

    var allocator: Box[Allocator];

    public static function new(allocator: Box[Allocator]): Self using implicit allocator {
        return struct Scene {
            allocator,
        };
    }

    public function destroy() using implicit this.allocator {
        this.entities.free();
    }

    public function update(context: Ptr[UpdateContext]) using implicit context {
        this.input.update();
        this.camera.update();
        this.onUpdate.invoke();
        for e in this.entities {
            e.update();
        }
        this.postUpdate.invoke();
    }

    public function render(context: Ptr[RenderContext]) {
        if this.bgAlpha > 0 {
            var key = struct DrawKey;
            context.renderSystem.addRect(key,
                struct Rectangle[Int] {
                    point: struct Point[Int],
                    dimensions: struct Dimensions[Int] {x: context.window.width, y: context.window.height},
                },
                struct Matrix,
                this.bgColor.withAlpha(this.bgAlpha), false
            );
        }
        context.camera = this.camera;
        this.onRender.invoke(context);
        for e in this.entities {
            var render = context.copy();
            e.render(render);
        }
        this.postRender.invoke(context);
    }
}
